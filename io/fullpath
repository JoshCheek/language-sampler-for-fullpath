#!/usr/bin/env io

helpScreenLines := method(list(
  "usage: fullpath *[relative-paths] [-c]",
  "",
  "  Prints the fullpath of the paths",
  "  If no paths are given as args, it will read them from stdin",
  "",
  "  If there is only one path, the trailing newline is omitted",
  "",
  "  The -c flag will copy the results into your pasteboard"
))

readPaths := method(
  File standardInput readLines select(line, line isEmpty not)
)

dir        := Directory currentWorkingDirectory .. "/"
paths      := list()
printHelp  := false
copyOutput := false

System args rest foreach(arg,
  (arg == "-h" or arg == "--help") ifTrue(printHelp  = true) or
  (arg == "-c" or arg == "--copy") ifTrue(copyOutput = true) or
  (arg isEmpty)                    ifFalse(paths append(arg))
)

printHelp ifTrue(
  helpScreenLines foreach(line, line println)
  System exit
)

(paths size == 0) ifTrue(paths = readPaths)
paths = paths map(index, arg, dir .. arg)

printPaths := method(paths, stream,
  (paths size == 1) ifTrue(
    stream write(paths first)
  ) ifFalse (
    paths foreach(path, stream write(path, "\n"))
  )
)

copyOutput ifTrue(
  prgm := SystemCall clone setCommand("pbcopy")
  prgm asyncRun(prgm command, prgm arguments, prgm environment)
  printPaths(paths, prgm stdin)
  prgm stdin close
)

printPaths(paths, File standardOutput)
