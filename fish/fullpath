#!/usr/bin/env fish

# process args into these three vars
set paths
set printHelp false
set copyOutput false

for arg in $argv
  switch $arg
  case -h --help
    set printHelp true
  case -c --copy
    set copyOutput true
  case '?*'
    set paths $paths $arg
  end
end

# maybe print help
if [ $printHelp = true ]
  echo "usage: fullpath *[relative-paths] [-c]"
  echo
  echo "  Prints the fullpath of the paths"
  echo "  If no paths are given as args, it will read them from stdin"
  echo
  echo "  If there is only one path, the trailing newline is omitted"
  echo
  echo "  The -c flag will copy the results into your pasteboard"
  exit 0
end

# read paths from stdin if none were provided
if [ (count $paths) = 0 ]
  while read -l line
    if test -n $line
      set paths $paths $line
    end
  end
end

# print them to stdout / pbcopy
function print_paths
  if [ (count $argv) = 1 ]
    echo -n $PWD/$argv[1]
  else
    for arg in $argv
      echo $PWD/$arg
    end
  end
end

if [ $copyOutput = true ]
  print_paths $paths | pbcopy
end
print_paths $paths
