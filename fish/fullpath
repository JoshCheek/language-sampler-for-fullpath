#!/usr/bin/env fish

# process args into these three vars
set paths
set printHelp false
set copyOutput false

for arg in $argv
  if test $arg = "-h"; or test $arg = "--help"
    set printHelp true
  else if test $arg = "-c"; or test $arg = "--copy"
    set copyOutput true
  else if test -n $arg
    set paths $paths $arg
  end
end

# maybe print help
if test $printHelp = true
  echo "usage: fullpath *[relative-paths] [-c]"
  echo
  echo "  Prints the fullpath of the paths"
  echo "  If no paths are given as args, it will read them from stdin"
  echo
  echo "  If there is only one path, the trailing newline is omitted"
  echo
  echo "  The -c flag will copy the results into your pasteboard"
  exit 0
end

# read paths from stdin if none were provided
if test (count $paths) -eq 0
  while read -l line
    if test -n $line
      set paths $paths $line
    end
  end
end

# print them to stdout / pbcopy
function print_paths
  if test (count $paths) -eq 1
    echo -n $PWD/$paths[1]
  else
    for arg in $paths
      echo $PWD/$arg
    end
  end
end

print_paths
if test true = $copyOutput
  print_paths | pbcopy
end
